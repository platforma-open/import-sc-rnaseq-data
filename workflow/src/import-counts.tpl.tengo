self := import("@platforma-sdk/workflow-tengo:tpl")

ll := import("@platforma-sdk/workflow-tengo:ll")
exec := import("@platforma-sdk/workflow-tengo:exec")
assets := import("@platforma-sdk/workflow-tengo:assets")
pConstants := import("@platforma-sdk/workflow-tengo:pframes.constants")

geneMap := import(":libs.gene-map")


// Import format-specific processors
formatXsv := import(":libs.processors.format-xsv")
formatH5ad := import(":libs.processors.format-h5ad")
formatMtx := import(":libs.processors.format-mtx")
formatSeurat := import(":libs.processors.format-seurat")

formatProcessors := {
	"xsv": formatXsv,
	"h5ad": formatH5ad,
	"mtx": formatMtx,
	"seurat": formatSeurat
}

self.defineOutputs(
	"rawCountsCsv",
	"normCountsCsv",
	"cellMetricsCsv"
)

self.body(func(inputs) {
	// Extract basic inputs
	inputData := inputs[pConstants.VALUE_FIELD_NAME]
	importMode := string(inputs.importMode)
	sampleId := inputs[pConstants.KEY_FIELD_NAME][0]

	// Determine file format
	fileFormat := ""
	if importMode == "mtx" {
		fileFormat = "mtx"
	} else if (importMode == "h5ad" || importMode == "h5ad-multi-sample") {
		fileFormat = "h5ad"
	} else if (importMode == "seurat" || importMode == "seurat-multi-sample") {
		fileFormat = "seurat"
	} else {
		fileFormat = "xsv"
	}

	inputMap := inputData.inputs()

	// Select processor based on file format
	processor := formatProcessors[fileFormat]
	if is_undefined(processor) {
		ll.panic("ERROR: Unsupported file format: %v", fileFormat)
	}

	// Build format-specific context using processor
	context := processor.buildContext(inputs, inputMap, geneMap)
	
	// Use provided memory if available, otherwise use calculated memory
	mem := !is_undefined(inputs.mem) ? string(inputs.mem) + "GiB" : string(inputs.mem) + "GiB"
	cpu := !is_undefined(inputs.cpu) ? inputs.cpu : 1

	countMatrixCsv := exec.builder()
	// Select software based on format
	if fileFormat == "seurat" {
		countMatrixCsv = countMatrixCsv.
			software(assets.importSoftware("@platforma-open/milaboratories.import-sc-rnaseq-data.software:counts-seurat"))
	} else {
		countMatrixCsv = countMatrixCsv.
			software(assets.importSoftware("@platforma-open/milaboratories.import-sc-rnaseq-data.software:counts-csv"))
	}
	
	// Generate count matrix CSV using processor
	countMatrixCsv = countMatrixCsv.
		mem(mem).
		cpu(cpu).
		arg("--format").arg(fileFormat)

	countMatrixCsv = processor.prepareCountMatrixArgs(countMatrixCsv, inputMap, context)

	countMatrixCsv = countMatrixCsv.
		arg("--sample-id").arg(sampleId).
		arg("--output").arg("counts.parquet").
		saveFile("counts.parquet").
		saveFile("counts_normalized.parquet").
		printErrStreamToStdout().
		run()

	rawCounts := countMatrixCsv.getFile("counts.parquet")
	normCounts := countMatrixCsv.getFile("counts_normalized.parquet")

	// Generate cell metrics CSV using processor
	cellMetricsCsv := exec.builder().
		software(assets.importSoftware("@platforma-open/milaboratories.import-sc-rnaseq-data.software:cell-metrics")).
		mem(mem).
		cpu(cpu).
		arg("--format").arg("parquet").
		arg("--input-file").arg("rawCounts.parquet").
		addFile("rawCounts.parquet", rawCounts).
		arg("--species").arg(inputs.species).
		arg("--sample-id").arg(sampleId).
		arg("--output").arg(".").
		saveFile("cell_metrics.csv").
		printErrStreamToStdout().
		run()

	// Filter cells based on metrics
	filteredCellsCountsCsv := exec.builder().
		software(assets.importSoftware("@platforma-open/milaboratories.import-sc-rnaseq-data.software:filter-cells")).
		mem(mem).
		cpu(cpu).
		addFile("rawCounts.parquet", rawCounts).
		addFile("rawCounts_normalized.parquet", normCounts).
		addFile("cell_metrics.csv", cellMetricsCsv.getFile("cell_metrics.csv")).
		arg("--raw_counts").arg("rawCounts.parquet").
		arg("--normalized_counts").arg("rawCounts_normalized.parquet").
		arg("--metrics").arg("cell_metrics.csv").
		arg("--sample-id").arg(sampleId).
		arg("--output_raw").arg("filtered_rawCounts.csv").
		arg("--output_normalized").arg("filtered_rawCounts_normalized.csv").
		saveFile("filtered_rawCounts.csv").
		saveFile("filtered_rawCounts_normalized.csv").
		printErrStreamToStdout().
		run()

	output := {
		rawCountsCsv: filteredCellsCountsCsv.getFile("filtered_rawCounts.csv"),
		normCountsCsv: filteredCellsCountsCsv.getFile("filtered_rawCounts_normalized.csv"),
		cellMetricsCsv: cellMetricsCsv.getFile("cell_metrics.csv")
	}

	return output

})
