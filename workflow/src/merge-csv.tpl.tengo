self := import("@platforma-sdk/workflow-tengo:tpl")
exec := import("@platforma-sdk/workflow-tengo:exec")
assets := import("@platforma-sdk/workflow-tengo:assets")
ll := import("@platforma-sdk/workflow-tengo:ll")
pConstants := import("@platforma-sdk/workflow-tengo:pframes.constants")
maps := import("@platforma-sdk/workflow-tengo:maps")
json := import("json")

self.defineOutputs("mergedCsv")

self.body(func(inputs) {
	inputData := inputs[pConstants.VALUE_FIELD_NAME]
	inputMap := inputData.inputs()

	ll.print("DBG: merge-csv - merging %v samples", len(inputMap))

	mergeCmdBuilder := exec.builder().
		software(assets.importSoftware("@platforma-open/milaboratories.import-sc-rnaseq-data.software:merge-csv")).
		arg("-o").arg("merged_counts.csv").
		saveFile("merged_counts.csv")

	for sKey in maps.getKeys(inputMap) {
		csvFile := inputMap[sKey]
		key := json.decode(sKey)
		if len(key) != 1 {
			ll.panic("malformed key: %v", sKey)
		}
		sampleId := key[0]
		fileName := sampleId + ".csv"
		mergeCmdBuilder.
			arg(fileName).
			addFile(fileName, csvFile)
	}

	mergeCmd := mergeCmdBuilder.run()
	mergedCsv := mergeCmd.getFile("merged_counts.csv")

	return {
		mergedCsv: mergedCsv
	}
})

