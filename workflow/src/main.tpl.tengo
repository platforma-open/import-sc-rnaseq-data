wf := import("@platforma-sdk/workflow-tengo:workflow")
assets := import("@platforma-sdk/workflow-tengo:assets")
render := import("@platforma-sdk/workflow-tengo:render")
smart := import("@platforma-sdk/workflow-tengo:smart")
pframes := import("@platforma-sdk/workflow-tengo:pframes")
ll := import("@platforma-sdk/workflow-tengo:ll")
maps := import("@platforma-sdk/workflow-tengo:maps")
json := import("json")

processTpl := assets.importTemplate(":process")
inferSpeciesTpl := assets.importTemplate(":infer-species")
splitH5adTpl := assets.importTemplate(":split-h5ad")

wf.prepare(func(args) {
	importMode := string(args.importMode)

	// Resolve the correct input based on import mode
	rawInput := undefined
	if importMode == "csv" {
		rawInput = wf.resolve(args.datasetRef, { errIfMissing: true })
	} else if importMode == "mtx" {
		rawInput = wf.resolve(args.mtxDatasetRef, { errIfMissing: true })
	} else if importMode == "h5ad" {
		rawInput = wf.resolve(args.h5adDatasetRef, { errIfMissing: true })
	} else {
		ll.panic("Unknown import mode: %v", importMode)
	}

	return {
		resolvedInput: rawInput,
		resolvedImportMode: importMode
	}
})

wf.body(func(args) {
	rawInput := args.resolvedInput
	importMode := args.resolvedImportMode
	blockId := wf.getBlockId()

	input := rawInput
	isMultiSampleH5ad := false
	if importMode == "h5ad" {
		spec := rawInput.spec
		isPFrameSpec := maps.containsKey(spec, "columns")

		if isPFrameSpec {
			// This is a PFrame
			for _, columnSpec in spec.columns {
				if columnSpec.name == "pl7.app/sequencing/data" {
					if len(columnSpec.axesSpec) == 1 && columnSpec.axesSpec[0].name == "pl7.app/sampleGroupId" {
						isMultiSampleH5ad = true
					}
					break
				}
			}
		} else {
			// This is a PColumn
			if maps.containsKey(spec, "axesSpec") && len(spec.axesSpec) == 1 && spec.axesSpec[0].name == "pl7.app/sampleGroupId" {
				isMultiSampleH5ad = true
			}
		}
	}

	if isMultiSampleH5ad {
		bundleBuilder := wf.createPBundleBuilder()
		bundleBuilder.addAnchor("main", args.h5adDatasetRef)
		bundleBuilder.addSingle({
			axes: [
				{ anchor: "main", idx: 0 },
				{ name: "pl7.app/sampleId", type: "String" }
			],
			name: "pl7.app/sequencing/data/sampleGroups"
		}, "linker")
		bundle := bundleBuilder.build()

		splitRun := render.createEphemeral(splitH5adTpl, {
			dataset: rawInput,
			bundle: bundle
		})
		input = splitRun.output("processedInput")
	}

	// First infer species
	inferSpeciesRun := render.createEphemeral(inferSpeciesTpl, {
		input: input.getFutureInputField("data"),
		inputSpec: input.getFutureInputField("spec"),
		importMode: importMode
	})

	processRun := render.createEphemeral(processTpl, {
		inputSpec: input.getFutureInputField("spec"),
		inputData: input.getFutureInputField("data"),
		species: inferSpeciesRun.output("species"),
		geneFormat: inferSpeciesRun.output("geneFormat"),
		importMode: importMode,

		params: smart.createJsonResource({
			blockId: blockId
			// mem: args.mem,
			// cpu: args.cpu
		})
	})

	return {
		outputs: {
			rawCountsPf: pframes.exportFrame(processRun.output("rawCountsPf")),
			cellMetricsPf: pframes.exportFrame(processRun.output("cellMetricsPf")),
			resultsSummaryPf: pframes.exportFrame(processRun.output("resultsSummaryPf"))
		},
		exports: {
			exports: processRun.output("exports"),
			cellMetricsPf: processRun.output("cellMetricsPf")
		}
	}
})

