maps := import("@platforma-sdk/workflow-tengo:maps")
json := import("json")
smart := import("@platforma-sdk/workflow-tengo:smart")
consts := import("@platforma-sdk/workflow-tengo:pframes.constants")
pColumn := import("@platforma-sdk/workflow-tengo:pframes.pcolumn")

RTYPE_P_COLUMN_DATA_JSON := consts.RTYPE_P_COLUMN_DATA_JSON

mapToPValueData := func(map) {
	data := {}
	for key, value in map {
		data[json.encode([key])] = value
	}
	result := {
		keyLength: 1,
		data: data
	}
	return result
}

createJsonPColumnData := func(data) {
	return smart.createValueResource(RTYPE_P_COLUMN_DATA_JSON, json.encode(mapToPValueData(data)))
}

parseSampleGroups := func(linker) {
	data := json.decode(linker.data.getData())
	result := {}
	for key, value in data {
		k := json.decode(key)
		if is_undefined(result[k[0]]) {
			result[k[0]] = {}
		}
		result[k[0]][k[1]] = value
	}
	return result
}

isMultiSample := func(spec) {
	isPFrameSpec := maps.containsKey(spec, "columns")

	if isPFrameSpec {
		for _, columnSpec in spec.columns {
			if columnSpec.name == "pl7.app/sequencing/data" {
				if len(columnSpec.axesSpec) == 1 && columnSpec.axesSpec[0].name == "pl7.app/sampleGroupId" {
					return true
				}
				break
			}
		}
	} else {
		if maps.containsKey(spec, "axesSpec") && len(spec.axesSpec) == 1 && spec.axesSpec[0].name == "pl7.app/sampleGroupId" {
			return true
		}
	}
	return false
}

createDatasetSpec := func(sampleIds, sampleIdAxis, fileExtension) {
	dsSpec := {
		kind: "PColumn",
		name: "pl7.app/sequencing/data",
		domain: maps.clone({
			"pl7.app/fileExtension": fileExtension,
			"pl7.app/compression": ""
		}, { removeUndefs: true }),
		valueType: "File",
		annotations: {
			"pl7.app/label": "Sequencing Data",
			"pl7.app/axisKeys/0": string(json.encode(sampleIds))
		},
		axesSpec: [sampleIdAxis]
	}
	return dsSpec
}

processMultiSample := func(bundle, importMode, fileExtension) {
	dataset := bundle.getColumn("main")
	linker := bundle.getColumns("linker")[0]
	sampleGroups := parseSampleGroups(linker)

	file_key := maps.getKeys(dataset.data.inputs())[0]
	file := dataset.data.inputs()[file_key]

	sampleIds := []
	samplesMap := {}
	for groupId, samples in sampleGroups {
		for sampleId, sampleName in samples {
			samplesMap[sampleId] = sampleName
			sampleIds = append(sampleIds, sampleId)
		}
	}

	sampleIdAxis := linker.spec.axesSpec[1]
	groupIdAxis := linker.spec.axesSpec[0]
	
	sampleColumnName := ""
	if maps.containsKey(groupIdAxis, "annotations") && maps.containsKey(groupIdAxis.annotations, "pl7.app/sampleColumnName") {
		sampleColumnName = groupIdAxis.annotations["pl7.app/sampleColumnName"]
	}
	
	dsSpec := createDatasetSpec(sampleIds, sampleIdAxis, fileExtension)

	dataBuilder := pColumn.resourceMapBuilder(1)
	for sampleId in sampleIds {
		dataBuilder.add([sampleId], file)
	}

	return {
		importMode: importMode,
		spec: dsSpec,
		data: dataBuilder.build(),
		samplesMap: samplesMap,
		sampleColumnName: sampleColumnName
	}
}

export {
	mapToPValueData: mapToPValueData,
	createJsonPColumnData: createJsonPColumnData,
	parseSampleGroups: parseSampleGroups,
	isMultiSample: isMultiSample,
	createDatasetSpec: createDatasetSpec,
	processMultiSample: processMultiSample
}

