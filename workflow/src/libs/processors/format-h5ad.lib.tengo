maps := import("@platforma-sdk/workflow-tengo:maps")
pConstants := import("@platforma-sdk/workflow-tengo:pframes.constants")

getFirstFile := func(inputMap) {
	firstKey := maps.getKeys(inputMap)[0]
	return inputMap[firstKey]
}

addH5adFile := func(builder, inputMap, context) {
	uniqueSampleData := getFirstFile(inputMap)
	return builder.
		addFile("inputCounts." + context.fileExtension, uniqueSampleData).
		arg("--h5ad").arg("inputCounts." + context.fileExtension)
}

addMultiSampleArgs := func(builder, context) {
	if context.importMode == "h5ad-multi-sample" {
		builder = builder.arg("--sample-name").arg(context.sampleName)
		if !is_undefined(context.sampleColumnName) && context.sampleColumnName != "" {
			builder = builder.arg("--sample-column-name").arg(context.sampleColumnName)
		}
	}
	return builder
}

buildContext := func(inputs, inputMap, geneMap) {
	sampleId := inputs[pConstants.KEY_FIELD_NAME][0]
	return {
		fileExtension: inputs.fileExtension,
		importMode: string(inputs.importMode),
		sampleName: inputs.samplesIdToLabelMap[sampleId],
		sampleColumnName: inputs.sampleColumnName,
		geneFormat: inputs.geneFormat,
		geneAsset: geneMap.getGeneAsset(inputs.species),
		geneAssetName: geneMap.getAssetName(inputs.species)
	}
}

prepareCountMatrixArgs := func(builder, inputMap, context) {
	builder = addH5adFile(builder, inputMap, context)
	builder = addMultiSampleArgs(builder, context)
	
	builder = builder.arg("--gene-format").arg(context.geneFormat)
	
	if context.geneFormat == "gene symbol" {
		builder = builder.
			addAsset(context.geneAsset, ".", context.geneAssetName).
			arg("--annotation").arg(context.geneAssetName)
	}
	
	return builder
}

export {
	buildContext: buildContext,
	prepareCountMatrixArgs: prepareCountMatrixArgs
}

