self := import("@platforma-sdk/workflow-tengo:tpl")
exec := import("@platforma-sdk/workflow-tengo:exec")
assets := import("@platforma-sdk/workflow-tengo:assets")

geneMap := import(":libs.gene-map")

self.defineOutputs("geneSymbolsCsv")

self.body(func(inputs) {
	species := inputs.species
	filteredGenesCountsCsvList := inputs.filteredGenesCountsCsvList

	geneAsset := geneMap.getGeneAsset(species)
	geneAssetName := geneMap.getAssetName(species)

	cmdBuilder := exec.builder().
		software(assets.importSoftware("@platforma-open/milaboratories.import-sc-rnaseq-data.software:map-genes")).
		mem("16GiB").
		cpu(1).
		addAsset(geneAsset, ".", geneAssetName).
		arg("--annotation").arg(geneAssetName).
		arg("--output").arg("geneSymbols.csv")

	// Add all input files
	for i, csvFile in filteredGenesCountsCsvList {
		fileName := "rawCounts_" + string(i) + ".csv"
		cmdBuilder.addFile(fileName, csvFile).arg("--raw_counts").arg(fileName)
	}

	geneSymbolsJob := cmdBuilder.
		saveFile("geneSymbols.csv").
		printErrStreamToStdout().
		run()

	return {
		geneSymbolsCsv: geneSymbolsJob.getFile("geneSymbols.csv")
	}
})
